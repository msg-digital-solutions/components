def nexusCredentials = usernamePassword(
	credentialsId: 'nexus-artifact-zl-credentials',
    usernameVariable: 'NEXUS_USER',
    passwordVariable: 'NEXUS_PASSWORD')

def gitCredentials = usernamePassword(
	credentialsId: 'github-credentials',
    usernameVariable: 'GITHUB_LOGIN',
    passwordVariable: 'GITHUB_TOKEN')

def dockerCredentials = usernamePassword(
	credentialsId: 'docker-registry-credentials',
    passwordVariable: 'DOCKER_PASSWORD',
    usernameVariable: 'DOCKER_LOGIN')

def artifactoryCredentials = usernamePassword(
    credentialsId: 'artifactory-datapwn-credentials',
    passwordVariable: 'ARTIFACTORY_PASSWORD',
    usernameVariable: 'ARTIFACTORY_LOGIN')

def sfCredentials = usernamePassword(
    credentialsId: 'tcompv0-salesforce-credentials',
    usernameVariable: 'SALESFORCE_USERNAME',
    passwordVariable: 'SALESFORCE_PASSWORD')

def sfSecurityToken = string(
    credentialsId: 'tcompv0-salesforce-account-key',
    variable: 'SALESFORCE_SECURITY_TOKEN')

def sfExpiredCredentials = usernamePassword(
    credentialsId: 'expired-salesforce-credentials',
    usernameVariable: 'EXPIRED_SALESFORCE_USERNAME',
    passwordVariable: 'EXPIRED_SALESFORCE_PASSWORD')

def sfExpiredSecurityToken = string(
    credentialsId: 'expired-salesforce-account-key',
    variable: 'EXPIRED_SALESFORCE_SECURITY_TOKEN')

def branch = env.BRANCH_NAME
def repo = 'components'
def slackChannel = 'components-ci'
def escaped_branch = branch.toLowerCase().replaceAll("/", "_")
def PRODUCTION_DEPLOYMENT_REPOSITORY = "snapshots"
def deploymentSuffix = (env.BRANCH_NAME == "master" || env.BRANCH_NAME.startsWith("maintenance/")) ? "${PRODUCTION_DEPLOYMENT_REPOSITORY}" : "dev_branch_snapshots/branch_${escaped_branch}"
def m2 = "/tmp/jenkins/tdi/m2/${deploymentSuffix}"
def podLabel = "components-${UUID.randomUUID().toString()}".take(53)
String repository = 'components'

pipeline {
  agent {
        kubernetes {
            label podLabel
            yaml """
apiVersion: v1
kind: Pod
spec:
    containers:
        -
            name: main
            image: artifactory.datapwn.com/tlnd-docker-dev/talend/common/tsbi/jdk8-svc-springboot-builder:1.14.0-2.1-20191203093421
            command: [cat]
            tty: true
            volumeMounts: [{name: m2main, mountPath: /root/.m2/repository}]
            resources: {requests: {memory: 3G, cpu: '2.5'}, limits: {memory: 3G, cpu: '2.5'}}
            env:
              - name: DOCKER_HOST
                value: tcp://localhost:2375
        -
            name: dind-daemon
            image: artifactory.datapwn.com/docker-io-remote/docker:23.0.1-dind
            resources:
                requests:
                    cpu: 2.5
                    memory: 3G
            securityContext:
                privileged: true
            volumeMounts:
              - name: docker-graph-storage
                mountPath: /var/lib/docker
            env:
              - name: DOCKER_TLS_CERTDIR
                value: ""
    volumes:
        -
            name: m2main
            hostPath: {path: ${m2} }
        -
            name: docker-graph-storage
            emptyDir: {}
    imagePullSecrets:
        - name: talend-registry
"""
        }
    }

  environment {
    MAVEN_OPTS = "-Dmaven.artifact.threads=128 -Dorg.slf4j.simpleLogger.showThreadName=true -Dorg.slf4j.simpleLogger.showDateTime=true -Dorg.slf4j.simpleLogger.dateTimeFormat=HH:mm:ss -Dtalend.maven.decrypter.m2.location=${WORKSPACE}/.jenkins/"
    ARTIFACTORY_REGISTRY = "artifactory.datapwn.com"
  }

  options {
    timeout(time: 120, unit: 'MINUTES')
    skipStagesAfterUnstable()
    buildDiscarder(logRotator(numToKeepStr: '10'))
    disableConcurrentBuilds()
  }

  parameters {
    choice(name: 'Action',
            choices: ['STANDARD', 'CHANGELOG'],
            description: '''
            Kind of running : 
            STANDARD (default),[ Compile | Test] for all branch build, and [ Nexus deployment ]for master and maintenance branch
            CHANGELOG for creating the release changelog. You have to call it after the release is done.
            (Other actions are under development)'''
    )
    booleanParam(
            name: 'DRAFT_CHANGELOG',
            defaultValue: true,
            description: '''
            Create a draft release changelog. User will need to approve it on github.
            Only used on release action''')
  }

  stages {
    stage('Prepare build') {
      steps {
        container('main') {
          script {
            echo 'Git login'
            withCredentials([gitCredentials]) {
              sh """
                            bash ci/git-login.sh \
                                "\${GITHUB_LOGIN}" \
                                "\${GITHUB_TOKEN}"
                        """
            }
          }
        }
      }
    }

    stage('Compile | Test') {
      when {
        expression { params.Action == 'STANDARD' }
      }
      steps {
        container('main') {
           withCredentials([gitCredentials, nexusCredentials, dockerCredentials, artifactoryCredentials, sfCredentials, sfSecurityToken, sfExpiredCredentials, sfExpiredSecurityToken]) {
             sh '''#!/bin/bash
             docker version
             echo $ARTIFACTORY_PASSWORD | docker login $ARTIFACTORY_REGISTRY -u $ARTIFACTORY_LOGIN --password-stdin
             mvn clean -B -U -V \
                -Dsalesforce.user=${SALESFORCE_USERNAME} \
                -Dsalesforce.password=${SALESFORCE_PASSWORD} \
                -Dsalesforce.key=${SALESFORCE_SECURITY_TOKEN} \
                -Dsalesforce.user.expired=${EXPIRED_SALESFORCE_USERNAME} \
                -Dsalesforce.password.expired=${EXPIRED_SALESFORCE_PASSWORD} \
                -Dsalesforce.key.expired=${EXPIRED_SALESFORCE_SECURITY_TOKEN} \
             install
             '''
           }
        }
      }
    }

    stage('Nexus Deployment') {
      when {
        anyOf {
          expression { params.Action != 'CHANGELOG' }
          expression { env.BRANCH_NAME == "master" }
          expression { env.BRANCH_NAME.startsWith('maintenance/') }
        }
      }
      steps {
        container('main') {
          withCredentials([gitCredentials, nexusCredentials, dockerCredentials, artifactoryCredentials]) {
            sh '''#!/bin/bash
            docker version
            echo $ARTIFACTORY_PASSWORD | docker login $ARTIFACTORY_REGISTRY -u $ARTIFACTORY_LOGIN --password-stdin
            mvn clean -B -U -V -s ci/settings.xml deploy -DskipTests -DskipITs
            '''
          }
        }
      }
    }

    stage('Changelog') {
      when {
        expression { params.Action == 'CHANGELOG' }
      }
      steps {
        container('main') {
          withCredentials([gitCredentials]) {
            script {
              final def pom = readMavenPom file: 'pom.xml'
              String currentVersion = pom.version.split('-')[0]
              String releaseVersion = evaluatePreviousVersion(currentVersion)
              String previousVersion = evaluatePreviousVersion(releaseVersion)
              sh """
                    bash ci/changelog.sh \
                        '${repository}' \
                        '${previousVersion}' \
                        '${releaseVersion}' \
                        '${params.DRAFT_CHANGELOG}' \
                        "\${BRANCH_NAME}" \
                        "\${GITHUB_LOGIN}" \
                        "\${GITHUB_TOKEN}"
                """
            }
          }
        }
      }
    }
  }

  post {
    success {
        slackSend(color: '#00FF00', message: "SUCCESSFUL: Job '${env.JOB_NAME} [${env.BUILD_NUMBER}]' (${env.BUILD_URL})", channel: "${slackChannel}")
    }
    failure {
        slackSend(color: '#FF0000', message: "FAILED: Job '${env.JOB_NAME} [${env.BUILD_NUMBER}]' (${env.BUILD_URL})", channel: "${slackChannel}")
    }
  }
}

/**
 * Evaluate previous SemVer version
 * @param version current version
 * @return previous version
 */
private static String evaluatePreviousVersion(String version) {
  def components = version.split('\\.')

  int major = components[0] as int
  int minor = components[1] as int
  int patch = components[2] as int

  if (patch > 0) {
    patch--
  } else {
    patch = 0
    if (minor > 0) {
      minor--
    } else {
      minor = 0
      if (major > 0) {
        major--
      } else {
        // Invalid state: Cannot calculate previous version if major version is already 0 or less
        throw new IllegalArgumentException("Invalid version: $version")
      }
    }
  }

  return "${major}.${minor}.${patch}"
}